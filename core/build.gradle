apply plugin: 'docker'

mainClassName = "lr.core.PadFs"

sourceSets {
    main {
        java {
            srcDir 'src/main/libs'
        }
    }
}

jar {
    baseName = 'core'
    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": mainClassName)
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

dependencies {
    compile 'com.beust:jcommander:1.48'
}

run {
    /* Can pass all the properties: */
    systemProperties System.getProperties()

    /* Need to split the space-delimited value in the exec.args */
    if(System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

task buildDocker(type: Docker) {
    push = false
    applicationName = "${jar.baseName}"
    dockerfile = file('src/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}